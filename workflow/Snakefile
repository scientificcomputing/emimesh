import os
import yaml
conda_env = "envs/environment.yml"

if len(config)==0:
    print("Please specify a configfile with 'snakemake --configfile filename'")
    exit()
    
name = config["name"]

raw_dir = "results/{name}/raw/"
processed_dir = "results/{name}/processed/"
surf_dir = "results/{name}/surfaces/"
mesh_dir = "results/{name}/meshes/"

raw_data_path = raw_dir + "raw.vtk"

processed_data_path = processed_dir + "processed.vtk"
csgtree = surf_dir + "csgtree.json"
cellsizeplot = processed_dir + "cellsizes.png"
imagestatistic = processed_dir + "imagestatistic.yml"

mesh_path = mesh_dir + "mesh.xdmf"
meshstatistic = mesh_dir + "meshstatistic.yml"
mesh_img = mesh_dir + "mesh.png"
surfaces_dir = directory(surf_dir)

size_to_ntasks =  lambda size: min(24, int(1 + (size/400)**2))
size_to_minutes = lambda size: int((size/500)**3 * 120 + 60)

def dict_to_optionsstring(d):
    s = ""
    for key, value in d.items():
        if isinstance(value, list):
            for value_i in value:
                s += f" --{key} {value_i}"
        else:
            s += f" --{key} {value}"
    return s

rule all:
    input:
        mesh_img.format(name=name),
        meshstatistic.format(name=name)

rule downloadImageData:
    output:
        rawdata=raw_data_path
    conda:
        conda_env
    params:
        options=dict_to_optionsstring(config["raw"])
    shell:
        """
        python workflow/scripts/download_data.py \
        {params.options} \
        --output {output.rawdata}
        """

rule processImageData:
    input:
        rawdata=raw_data_path
    output:
        #imagestatistic,
        outfile=processed_data_path,
    conda:
        "envs/environment.yml"
    resources:
        ntasks=lambda wildcards: size_to_ntasks(config["raw"]["size"] / config["processing"]["dx"]),
        time=30,
    params:
        options=dict_to_optionsstring(config["processing"])
    shell:
        """
        python workflow/scripts/process_image_data.py --infile {input.rawdata} \
        {params.options} \
        --output {output.outfile} \
        --nworkers {resources.ntasks}
        """

rule extractSurfaces:
    input:
        processeddata=processed_data_path
    output:
        outdir=surfaces_dir,
        csgtree=csgtree
    resources:
        time=120,
        ntasks=lambda wildcards: size_to_ntasks(config["raw"]["size"] / config["processing"]["dx"]),
    conda:
        conda_env
    shell:
        """
        python workflow/scripts/extract_surfaces.py --infile {input.processeddata} --ncpus {resources.ntasks} \
        --outdir {output.outdir}
        """

rule generateMesh:
    input:
        csgtree=csgtree,
    output:
        outfile=mesh_path,
    conda:
        conda_env
    resources:
        ntasks=lambda wildcards: size_to_ntasks(config["raw"]["size"] / config["processing"]["dx"]),
        time=lambda wildcards: size_to_minutes(config["raw"]["size"] / config["processing"]["dx"]),
    threads: lambda wildcards: size_to_ntasks(config["raw"]["size"] / config["processing"]["dx"]),
    params:
        options=dict_to_optionsstring(config["meshing"])
    shell:
        """
        python workflow/scripts/generate_mesh.py \
        --csgtree {input.csgtree} \
        {params.options} \
        --output {output.outfile} \
        --max_threads {resources.ntasks} \
        """

#   python workflow/scripts/seperate_touching_cells.py --infile {output.outfile} --output {output.outfile}


rule evaluateMesh:
    input:
        mesh_path
    output:
        meshstatistic
    conda:
        conda_env
    threads: 1
    shell:
        """
        python workflow/scripts/evaluate_mesh.py \
        --infile {input} --output {output}
        """

rule takeScreenshot:
    input:
        mesh_path
    output:
        mesh_img
    conda:
        conda_env
    threads: 1
    shell:
        """
        python workflow/scripts/generate_screenshot.py \
        --infile {input} --output {output}
        """

rule generateAnalysisPlot:
    input:
        infile=imagestatistic
    output:
        plotfile=cellsizeplot
    conda:
        conda_env
    shell:
        """
        python workflow/scripts/generate_analysis_plots.py \
        --infile {input.infile} --output {output.plotfile}
        """

